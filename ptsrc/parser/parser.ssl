%  PT Parser.ssl        v1.02
%
% Date:  30 Jun 1980
%        (Revised 19 December 2017)
%
% Author:  Alan Rosselet
%
% Abstract
%
%     This module parses the syntax token stream emitted by the scanner
%   and emits a postfix stream of tokens to the semantic analyzer
%   module.  The parser detects and repairs syntax errors so that the
%   token stream emitted to the semantic analyzer is syntactically
%   legal (except for the special case of declaration phase errors - see
%   comment in the Block routine below).

Input :
        % Keywords must be first both here and in stdIdentifiers,
        % and they must appear in the same order

        pDiv                    'div'
        firstKeywordToken = pDiv
        pMod                    'mod'
        pOr                     'or'
        pAnd                    'and'
        pNot                    'not'
        pElse                   'else'
        pOf                     'of'
        pEnd                    'end'
        pArray                  'array'
        pFile                   'file'
        pPacked                 'packed'
        pVar                    'var'
        pIf                     'if'
        pExt                    'ext'  % Removed old PT keywords and added keywords seen on lines 35-45
        pCon                    'con'
        pTyp                    'typ'
        pDef                    'def'
        pPub                    'pub'
        pElif                   'elif'
        pSel                    'sel'
        pIs                     'is'
        pOn                     'on'
        pBreak                  'break'
        pLen                    'len'        
        pRep                    'rep'
        lastKeywordToken = pRep 

        pIdentifier 
        firstCompoundInputToken = pIdentifier
        pInteger
        pLiteral
        lastCompoundInputToken = pLiteral

        pNewLine
        pEndFile
        pPlus                   '+'
        pMinus                  '-'
        pStar                   '*'
        pPlusEqual              '+='  % Added Quby plus equals token and remove colon equals := token.
        pMinusEqual             '-='  % Added Quby minus equals token
        pDoubleEqual            '=='  % Added Quby double equals token
        pLeftBrace              '{'   % Added Quby left brace token
        pRightBrace             '}'   % Added Quby right brace token
        pDot                    '.'
        pComma                  ','
        pSemicolon              ';'
        pColon                  ':'
        pEquals                 '='
        pNotEqual               '!='
        pLess                   '<'
        pLessEqual              '<='
        pGreaterEqual           '>='
        pGreater                '>'
        pLeftParen              '('
        pRightParen             ')'
        pLeftBracket            '['
        pRightBracket           ']'
        pBang                   '#'   % Added Quby non-compound bang token
        pDotDot                 '..'
        lastSyntaxToken = pDotDot;

Output :
        sIdentifier 
        firstSemanticToken = sIdentifier
        firstCompoundSemanticToken = sIdentifier
        sInteger
        sLiteral
        lastCompoundSemanticToken = sLiteral

        sProgram
        sParmBegin
        sParmEnd
        sConst
        sType
        sVar
        sProcedure
        sBegin
        sEnd
        sNegate
        sArray
        sPacked
        sFile
        sRange
        sSelStmt   % Changed from sCaseStmt to sSelStmt for Quby select statements
        sSelEnd    % Changed from sCaseEnd to sSelEnd
        sLabelEnd
        sExpnEnd
        sNullStmt
        sAssignmentStmt
        sSubscript
        sCallStmt
        sFieldWidth
        sIfStmt
        sThen
        sElse
        sRepStmt   % Added sRepStmt and sRepOnBreak, removed PT sWhileStmt, sRepeatStmt, and sRepeatEnd
        sRepOnBreak
        sEq
        sNE
        sLT
        sLE
        sGT
        sGE
        sAdd
        sSubtract
        sMultiply
        sDivide
        sModulus
        sInfixOr
        sOr
        sInfixAnd
        sAnd
        sNot
        sModule    % Added Quby sModule and sPublic for Quby modules
        sPublic 
        sSubstring % Added sSubstring and sLen for Quby string operations
        sLen
        sNewLine
        sEndOfFile
        lastSemanticToken = sEndOfFile;


rules

Program :
        % Rule to handle Quby 'ext' keyword for starting a Quby program block.
        % Usage as follows: 
        % ext identifier {,identifier}
        % {declaration}
        % {statement}        
        'ext' .sProgram % Quby programs begin with 'ext' not 'program', and do not have a program name, only parameters.        

        % program parameters
        {
            pIdentifier  .sIdentifier
            [
                | ',':
                | *:
                    >
            ]
        }
        .sParmEnd
        @Block; % Quby has no semicolons and does not use periods after end of program. 

Block :
        % Block rule handles all declarations (con, typ, var, mod blocks, and def procedure blocks).
        % Quby requires all declarations to be complete before starting a statements block.

        {[
            | 'con':
                .sConst
                @ConstantDefinitions
            | 'typ':
                .sType
                @TypeDefinitions
            | 'var':
                .sVar
                @VariableDeclarations
            | 'mod':                      % Add in handling of Quby Modules
                .sModule                  % Module name
                pIdentifier .sIdentifier
                @Block                    % Remember modules can contain declarations and statements blocks just like a regular code block.
                'end'
            | 'def':
                .sProcedure
                % procedure name
                [
                    | 'pub':
                        .sPublic
                    | *:
                ]
                pIdentifier  .sIdentifier
                @ProcedureHeading
                @Block
                'end'
            | ';':
            | *:
                >
        ]}
        % Quby no longer uses 'begin' at beginning of code blocks but should still output .sBegin
        @Statements;

ConstantDefinitions :
        % Accept one or more named constant definitions
        pIdentifier  .sIdentifier
        @ConstantValue;  % Remove ability to have multiple constant type declaration with only one keyword. Quby requires 'con' keyword to be repeated for each declaration.

ConstantValue :
        '='
        [
            | pInteger:
                .sInteger
            | pIdentifier:
                .sIdentifier
            | '-':
                @UnsignedIntegerConstant
                .sNegate
            | '+':
                @UnsignedIntegerConstant
            | pLiteral:
                .sLiteral
        ];

UnsignedIntegerConstant :
        [
            | pIdentifier:
                .sIdentifier
            | pInteger:
                .sInteger
        ];

TypeDefinitions :
        % Accept one or more named type definitions.
        pIdentifier  .sIdentifier
        ':'  @TypeBody % Look for colon, and no longer expect semicolon
        ; % Remove ability to have multiple declarations under one 'typ' keyword

TypeBody :
        [
            | 'packed':
                'array'  .sArray  .sPacked
                '['  @SimpleType  ']'
                'of'  @SimpleType
            | 'array':
                .sArray
                '['  @SimpleType  ']'
                'of'  @SimpleType
            | 'file':
                .sFile
                'of'  @SimpleType
            | *:
                @SimpleType
        ];

SimpleType :
        % A simple type is a named type (possibly predefined)
        % or a subrange type specification
        [
            | pIdentifier:
                .sIdentifier
                [
                    | '..':
                        .sRange        
                        @OptionallySignedIntegerConstant
                    | *:
                ]
            | *:
                @OptionallySignedIntegerConstant
                '..'  .sRange
                @OptionallySignedIntegerConstant
        ];

OptionallySignedIntegerConstant :
        [
            | '-':
                @UnsignedIntegerConstant
                .sNegate
            | '+':
                @UnsignedIntegerConstant
            | *:
                @UnsignedIntegerConstant
        ];

VariableDeclarations :
        % Accept one or more variable declarations.
        pIdentifier  .sIdentifier
        ':'  @TypeBody  % don't expect semicolon
        ; % Remove ability to have multiple var declarations after each other         

ProcedureHeading :
        % Accept zero or more procedure formal parameter declarations.
        [
            | '(':
                {
                    % formal parameter identifier
                    [
                        | 'var':
                            pIdentifier  .sIdentifier
                            .sVar
                        | *:
                            pIdentifier  .sIdentifier
                    ]
                    ':'
                    % type identifier
                    pIdentifier  .sIdentifier
                    [
                        | ',':
                        | *:
                            >
                    ]
                }
                ')'
            | *:
        ]
        .sParmEnd;

Statements :
        % New statements rule that identifies a statement as a line that matches one of the following cases:
        % 1. Starts with an identifier (assignment or call statement)
        % 2. Starts with 'if' (if statement)
        % 3. Starts with 'sel' (sel statement)
        % 4. Starts with 'rep' (rep statement)
        % 5. Starts with ';' (empty statement)
        %
        % Remember, Quby allows multiple statements in a statements block as long as they are consecutive.

        .sBegin
        {[
             | pIdentifier:
                 @AssignmentOrCallStmt
             | 'if':
                 @IfStmt
             | 'sel':
                 @SelStmt
             | 'rep':
                 @RepStmt
             | ';':
             | *:
                 >
        ]}
        .sEnd;

AssignmentOrCallStmt :
        % Rule used for Quby assignment and call statements.
        % Changes were implemented to use Quby's assignment operator '='
        % as well as for implementing iterative assignments like '+=', and '-='. 

        [
            | '=':
                .sAssignmentStmt
                .sIdentifier    % identifier accepted in the Statements rule
                @Expression
                .sExpnEnd
            | '+=':
                .sAssignmentStmt
                .sIdentifier    % identifier accepted in the Statements rule
                .sIdentifier
                @Expression
                .sAdd
                .sExpnEnd
            | '-=':
                .sAssignmentStmt
                .sIdentifier    % identifier accepted in the Statements rule
                .sIdentifier
                @Expression
                .sSubtract
                .sExpnEnd
            | '[':
                .sAssignmentStmt
                .sIdentifier
                .sSubscript
                @Expression
                .sExpnEnd
                ']'  '='
                @Expression
                .sExpnEnd
            | *:
                @CallStmt
        ];

CallStmt :
        .sCallStmt
        .sIdentifier    % procedure identifier accepted in the Statements rule
        % accept zero or more actual parameters
        [
            | '(':
                {
                    @Expression
                    .sExpnEnd
                    % allow a field width specification to occur in calls
                    % to the standard procedures write and writeln
                    [
                        | ':':
                            .sFieldWidth
                            @Expression
                            .sExpnEnd
                        | *:
                    ]
                    [
                        | ',':
                        | *:
                            >
                    ]
                }
                ')'
            | *:        % no actual parameters
        ]
        .sParmEnd;

IfStmt :
        % Rule to handle Quby if statements.
        % Changes made to handle elif statements similar to python's handling.
        % Parser will pretend an elif statement is an else statement with a nested if statement to allow the PT semantic phase to be unaffected.

        .sIfStmt
        @Expression
        .sExpnEnd
        .sThen
        ':'
        @Statements
        [
            | 'else':
                .sElse
                ':'
                @Statements
                'end'
            | 'elif':
                .sElse
                @IfStmt
            | 'end':
            | *:
        ];

SelStmt :
        % New sel statement rule used for Quby sel statement parsing.
        % The rule differs from PT case statement implementation due to the addition of an 'else' block.
        % The 'else' block is optional but will act as a default case if none of the alternatives match.

        .sSelStmt
        @Expression
        .sExpnEnd
        ':'
        'is'
        @SelAlternative
        {[
            | 'else':
                .sElse
                ':'
                @Statements
            | 'is':
                @SelAlternative
            | pEnd:
                >
        ]}
        .sSelEnd;

SelAlternative :
        % A select alternative is a statement labelled by
        % one or more optionally signed integer constants

        {
            @OptionallySignedIntegerConstant
            [
                | ',':
                | *:
                    >
            ]
        }
        .sLabelEnd
        ':'  @Statements; % Calling Statements allows for multiple statements

RepStmt :
        % Rule used for parsing Quby 'rep' loop statements.
        % Quby 'rep' statements follow the following syntax:
        % rep
        %     {statement}
        %     on expression : breal
        %     {statement}
        % end
        %
        % Note that parsing only allows a single "on expression : break" statement to be valid Quby statement.

        .sRepStmt
        {
            @Statements
            [
                | 'on':
                    .sRepOnBreak
                    @Expression
                    ':'
                    'break'
                    .sExpnEnd
                | 'end':
                   >
            ]
        };

Expression :
        @SimpleExpression
        [
            | '==':
                @SimpleExpression  .sEq
            | '!=':
                @SimpleExpression  .sNE
            | '<':
                @SimpleExpression  .sLT
            | '<=':
                @SimpleExpression  .sLE
            | '>':
                @SimpleExpression  .sGT
            | '>=':
                @SimpleExpression  .sGE
            | *:
        ];

SimpleExpression :
        [
            | '+':
                @Term
            | '-':
                @Term  .sNegate
            | *:
                @Term
        ]
        {[
            | '+':
                @Term  .sAdd
            | '-':
                @Term  .sSubtract
            | 'or':
                .sInfixOr  @Term  .sOr
            | *:
                >
        ]};

Term :
        % Term rule altered to add Quby substring capability using pLiteral {int:int} syntax.
        % Substring operators are on the same precedence as div and mod in Quby.

        @Factor
        {[
            | '*':
                @Factor  .sMultiply
            | 'div':
                @Factor  .sDivide
            | 'mod':
                @Factor  .sModulus
            | '{':
                @Substring
            | 'and':
                .sInfixAnd  @Factor  .sAnd
            | *:
                >
        ]};

Substring:
        % New parsing rule to allow for Quby substring operations.
        % The general form of a substring in Quby is:
        % expression { expression : expression }        

        @Expression
        ':'
        @Expression
        '}'
        .sSubstring;              

Factor :
        % Factor rule was altered to add the Quby string length operator; 'len'.
        % The 'len' operator in Quby has the same precedence to that of the 'not' operator.
        % The general form of the string length operator is: len expression

        [
            | pIdentifier:
                .sIdentifier
                @IdentifierExtension
            | pInteger:
                .sInteger
            | '(':
                @Expression  ')'
            | 'not':
                @Factor
                .sNot
            | pLen:
                @Factor
                .sLen
            | pLiteral:
                .sLiteral
            | 'file':
                .sFile '(' @Expression ')'
                .sExpnEnd
        ];

IdentifierExtension :
        % Handle subscripts and function actual parameter lists
        [
            | '[':
                .sSubscript
                @Expression  ']'
                .sExpnEnd
            | '(':
                .sParmBegin
                @Expression  ')'
                .sExpnEnd
                .sParmEnd
            | *:
        ];

end
